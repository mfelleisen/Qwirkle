#! /bin/sh
#! generate the README.md files from README.source files, *.txt files, and purpose statements 
#| -*- racket -*-
exec racket -tm "$0" -- ${1+"$@"}
|#
#lang racket

;; create the README.md files from README.source in the top-level & all git-tracked code directories:
;; (assume the directories contain only files) 

(define EXCEPTIONS
  '[".git" "compiled" "README.md" "README.source" "info.rkt" "scribblings" ".DS_Store"]) 

#; {type Ordering = [Listof [directory:String ∪ [Cons directory:String [Listof file:String]]]]}
;; the `file` name is assumed to be a ".rkt" file, mostly except
;; a file name ending in * is a script 

(define ORDER ; Ordering 
  '[ ["Run" "xserver*" "xclients*"]
     ["Server" "server"]
     ["Referee" "referee" "ref-state"]
     ["Common"
      "player-interface"
      "placement"
      "q-rule"
      "game-state" "state-of-player"
      "map" "coordinates" "tiles"]
     "Client"
     ["Player" "mechanics" "strategies"]
     ["Integration" "xtest*" "xserver*" "xclients*"]])

;; ---------------------------------------------------------------------------------------------------
(define help-string
  #<< eos
 ./xreadme                  ;; shows the generated README.md file                    
 ./xreadme --no             ;; doesn't                                               
 ./xreadme --show directory ;; shows only the README.md in the specified directory
 ./xreadme --show ./        ;; shows only the README.md in the top-level directory   
 ./xreadme --help           ;; show this help string 
 eos
  )

(provide main)

(define (main . x)
  ;; -- analyze cmd line args and directory 
  (define show
    (match x
      [(list)              (eq? (system-type) 'macosx)]
      [(list "--no")       #false]
      [(list "--show" dir) (and (eq? (system-type) 'macosx) (directory-exists? dir) dir)]
      [(list "--help")     (printf "usage:\n~a\n" help-string) (exit 0)]
      [_ (printf "usage: \n~a\n" help-string) (exit 1)]))
  (define adirs (order-via (all-git-tracked directory-exists? EXCEPTIONS) ORDER))
  ;; -- now create the READMEs in `adirs` 
  (readme* adirs show)
  ;; ... finally and link them up
  ;; (must be in this order because the next expression assumes their existence)
  (top-level-readme adirs show))

#;{type Show = (U Boolean PathString)}

;; ---------------------------------------------------------------------------------------------------
;; readme creation for the top-level directory and its immediate sub-directories 

#; {[Listof PathString] Show -> Void}
(define (top-level-readme adirs show)
  (define files (map (λ (d) (build-path d "README.md")) adirs))
  (define purps (purpose-statements files identity))
  (define afils (map row files adirs purps))
  (define show2 (and show (or (boolean? show) (equal? show "./"))))
  (define table (string-append (make-table "directory" afils) (create-file-table "./")))
  (write-readme-and-show table show2))

#; {[Listof PathString] Boolean -> Void}
;; find README.source in each directory in `adirs` and create README.md in this directory
(define (readme* dir* show)
  (for ([dir dir*])
    (parameterize ([current-directory dir])
      (define show2 (and show (or (and (boolean? show) show) (and (string? show) (equal? show dir)))))
      (write-readme-and-show (create-file-table dir) show2))))

#; {PathString -> Table}
(define (create-file-table dir)
  (define files (order-via (all-git-tracked file-exists? EXCEPTIONS) (find-order dir ORDER)))
  (define purps (purpose-statements files strip-semis))
  (define afils (map row files files purps))
  (define table (make-table "file" afils))
  table)

#; {String [Listof Documented] [String -> String] Boolean -> Void}
;; create one README.md file with an organization table made from `afils` labeled with `header`
;; `clean` each description line with 
(define (write-readme-and-show table show)
  (copy-file2 "README.source" "README.md" 'delete-existing-one)
  (with-output-to-file "README.md"
    #:exists 'append
    (λ () (printf "~a" table)))
  (when show (system "open README.md")))

;; ---------------------------------------------------------------------------------------------------
;; extract purpose statements from code files, note missing purpose statements

(define SEMIs ";; ")
(define BANGs "#! ")

#; {[Listof PathString] -> [Listof String]}
(define (purpose-statements file* clean)
  (for/list ([file file*])
    (with-handlers ([exn:fail? (missing-purpose-statement file)])
      (cond
        [(regexp-match #px".rkt|.md" file) ;; code or readme file 
         (with-input-from-file file (λ () (clean (string-trim (caddr (port->lines))))))]
        [(regexp-match #px"x.*" file) ;; script
         (with-input-from-file file (λ () (clean (string-trim (cadr (port->lines))))))]
        [else ""]))))

#; {[List PathString String] -> Empty}
(define ((missing-purpose-statement file) xn)
  (error 'purpose-statement "~a \n~a" file (exn-message xn)))

#; {String -> String}
#; (strip-semis ";; ...") ;; yields "..."
(define (strip-semis line)
  (unless (or (equal? (substring line 0 3) SEMIs) (equal? (substring line 0 3) BANGs))
    (error 'strim-semis "missing semicolons for purpose statement in Racket file; given ~s" line))
  (substring line 3))

;; ---------------------------------------------------------------------------------------------------
;; interpret readme files to include diagrams 

#; {String[Filename] String[Filename] Any -> Void}
;; copy the content from `old` to `new`
;; replace "@@include(.)" with the content of the specified fle 
(define (copy-file2 old new _)
  (define c-old (file->list old read-line))
  (define c-new (replace "@@include" c-old))
  (with-output-to-file new #:exists 'replace (λ () (for ((l c-new)) (displayln l)))))

#; {String String[Filename] Any -> Void}
(define (replace _ c-old)
  (for/fold ([new '()] #:result (reverse new)) ([line c-old])
    (define m (regexp-match #px"@@include\\((.*)\\)" line))
    (cond
      [(boolean? m) (cons line new)]
      [else
       (define c (reverse (file->list (second m) read-line)))
       (append (code-block c) new)])))

#; {[Listof String] -> [Listof String]}
;; surround given list of strings with ```
(define (code-block c)
  (append (cons "```" c) (list "```")))

;; ---------------------------------------------------------------------------------------------------
;; connect with git and get tracked files and directories 

#; {-> [Listof String]}
(define (all-git-tracked exists? except)
  (define is-tracked? (make-git-tracked exists?))
  (for*/list ([fd (directory-list)]
              [fd-str (in-value (path->string fd))]
              #:when (and (is-tracked? fd-str) (not (member fd-str except))))
    fd-str))

#; {(String -> Boolean) PathString -> PathString -> Boolean}
(define (make-git-tracked exists?) 
  (define untracked (git-untracked))
  (lambda (fd)
    (and (exists? fd)
         (not (regexp-match #px"\\.+compiled" fd))
         (not (regexp-match #px"\\.+txt" fd))
         (not (member fd untracked)))))

#; {[Path] -> [Listof PathString]}
;; a primitive way to determine Untracked directories and files from the CURRENT DIRECTORY
(define (git-untracked)
  (match-define (list in out pid err control) (process "git status"))
  (define status (port->list read-line in))
  (let loop ((status status))
    (cond
      [(empty? status) '()]
      [(regexp-match #px"Untracked" (first status))
       (let inner ([status (cdddr status)])
         (define next (string-trim (first status)))
         (cond
           [(equal? "" next) '()]
           [else (cons next (inner (rest status)))]))]
      [else (loop (rest status))])))

;; ---------------------------------------------------------------------------------------------------
;; ordering of directories and files 

#; {[Listof String] Ordering -> [Listof Striing]}
(define (order-via givens0 order0)
  (let in-order ([order order0] [givens givens0])
    (match order 
      ['() givens]
      [(cons (cons (? string? dir) files) order+)
       (unless (member dir givens0)
         (error 'xreadme "specified name not found: ~a\n in ~s" dir givens0))
       (cons dir (in-order order+ (remove dir givens)))]
      [(cons (? string? dir) order+)
       (unless (member dir givens0)
         (error 'xreadme "specified name not found: ~a\n in ~s" dir givens0))
       (cons dir (in-order order+ (remove dir givens)))])))

#; {dir:String Ordering -> [Listof file:String]}
(define (find-order dir order)
  (match order
    ['() '()]
    [(cons (? string? d) order+)
     (if (equal? d dir) '[] (find-order dir order+))]
    [(cons (cons (? string? d) files) order+)
     (if (equal? d dir) (file-spec* files) (find-order dir order+))]))

#; {file-name:String -> file-name:String}
(define (file-spec* files)
  (for/list ([f files])
    (cond
      [(regexp-match "(.*)\\*$" f) => second]
      [else (~a f ".rkt")])))

;; ---------------------------------------------------------------------------------------------------
;; Some MARK DOWN facilities

#; {Type Table = String}

(struct row [path name purpose] #:prefab)
#; {type Row = [documented PathString String String]}

#; {String [Listof [List PathString String]] [Listof String] -> String}
(define (make-table header0 documented-files)
  (define header (make-header header0))
  (define content
    (for/list ([d documented-files])
      (match-define (row path name purpose) d)
      (~a "| [" name "](" path ")" " | " purpose " | \n")))
  (apply string-append header content))

(define (make-header x)
  (string-append
   "\n"
   (string-append "| " x " | purpose |\n")
   "|--------------------- | ------- |\n"))

;; ---------------------------------------------------------------------------------------------------
(module+ test
  (main "--show" "Common"))
